#compdef maid

_maid_tasks() {
  local -a tasks
  tasks=(${(@f)"$(maid -l 2>/dev/null | sed 's/ \(.\)/:\L\1/')"})

  if ((${#tasks[@]} > 0)) {
    _describe 'task' tasks
  } else {
    _message 'task (none found)'
  }
}

_maid() {
  _arguments \
    "(help)"{-h,--help}"[display usage]" \
    "(list)"{-l,--list}"[list available tasks concisely]" \
    "(dry-run)"{-n,--dry-run}"[don't run anything, only display commands]" \
    "(quiet)"{-q,--quiet}"[don't display anything]" \
    "(taskfile)"{-f,--taskfile=}"[use tasks in file]:maidfile:_files" \
    "(watch)"{-w,--watch=}"[watch tasks in path]:watch:_files" \
    "(restart)"{-r,--restart}"[restart tasks instead of waiting]" \
    ":task:_maid_tasks" \
    "*:args:_default"
}

_maid "$@"
